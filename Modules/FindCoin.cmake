INCLUDE(FindPackageHandleStandardArgs)

FILE(
	GLOB
	COIN_INCLUDE_PATHS
	$ENV{COINDIR}/include
	$ENV{COIN3DDIR}/include
	$ENV{HOME}/include
	/usr/local/include
	/usr/include/Coin*
	/usr/include
	$ENV{SystemDrive}/Coin*/include
	$ENV{ProgramW6432}/Coin*/include
	$ENV{ProgramFiles}/Coin*/include
)

FILE(
	GLOB
	COIN_LIBRARY_PATHS
	$ENV{COINDIR}/lib
	$ENV{COIN3DDIR}/lib
	$ENV{HOME}/lib
	/usr/local/lib
	/usr/lib
	$ENV{SystemDrive}/Coin*/lib
	$ENV{ProgramW6432}/Coin*/lib
	$ENV{ProgramFiles}/Coin*/lib
)

FIND_PATH(
	COIN_INCLUDE_DIRS
	NAMES
	Inventor/So.h
	HINTS
	${COIN_INCLUDE_PATHS}
)

FIND_LIBRARY(
	COIN_LIBRARY_DEBUG
	NAMES
	Coind coin2d coin3d
	HINTS
	${COIN_LIBRARY_PATHS}
)

FIND_LIBRARY(
	COIN_LIBRARY_RELEASE
	NAMES
	Coin coin2 coin3
	HINTS
	${COIN_LIBRARY_PATHS}
)

IF(WIN32)
	SET(COIN_DEFINITIONS -DCOIN_NOT_DLL)
ENDIF(WIN32)

IF(COIN_LIBRARY_DEBUG AND NOT COIN_LIBRARY_RELEASE)
	SET(COIN_LIBRARIES ${COIN_LIBRARY_DEBUG})
ENDIF(COIN_LIBRARY_DEBUG AND NOT COIN_LIBRARY_RELEASE)

IF(COIN_LIBRARY_RELEASE AND NOT COIN_LIBRARY_DEBUG)
	SET(COIN_LIBRARIES ${COIN_LIBRARY_RELEASE})
ENDIF(COIN_LIBRARY_RELEASE AND NOT COIN_LIBRARY_DEBUG)

IF(COIN_LIBRARY_DEBUG AND COIN_LIBRARY_RELEASE)
	SET(COIN_LIBRARIES debug ${COIN_LIBRARY_DEBUG} optimized ${COIN_LIBRARY_RELEASE})
ENDIF(COIN_LIBRARY_DEBUG AND COIN_LIBRARY_RELEASE)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(
	Coin
	DEFAULT_MSG
	COIN_INCLUDE_DIRS
	COIN_LIBRARIES
)

MARK_AS_ADVANCED(
	COIN_DEFINITIONS
	COIN_INCLUDE_DIRS
	COIN_LIBRARIES
	COIN_LIBRARY_DEBUG
	COIN_LIBRARY_RELEASE
) 
